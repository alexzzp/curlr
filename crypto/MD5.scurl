|| MD5.scurl
|| maintained by: sadman
|| Implementation of an MD5 message digest "derived from the RSA Data Security, Inc. MD5 Message-Digest Algorithm"
||
|| Classes:
||  sadMD5-Digester: 
||  sadMD5-Digest
||  sadMD5-ByteInputStream
||  sadMD5-ByteOutputStream
|| 
|| Distributed under the New BSD License:
|#
  Copyright (c) 2006, Steve Adams
  All rights reserved.

  Redistribution and use in source and binary forms, with or without modification, are permitted 
  provided that the following conditions are met:

    * Redistributions of source code must retain the above copyright notice, this list of conditions 
      and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright notice, this list of conditions 
      and the following disclaimer in the documentation and/or other materials provided with the distribution.
    * Neither the name of the <ORGANIZATION> nor the names of its contributors may be used to endorse or promote products 
      derived from this software without specific prior written permission.

  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED 
  WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A 
  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY 
  DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, 
  PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) 
  HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT 
  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, 
  EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#|

{curl-file-attributes character-encoding = "utf8"}
{doc-next
    {purpose A MD5 message digest.}
    {details
        {code sadMD5-Digest} objects may be created using
        a {docref sadMD5-Digester}.
    }
}

{define-class public final sadMD5-Digest 
  {inherits {MsgDigest-of-len 4}}
  
  || init
  {doc-next
    {purpose Initialize the digest.}
    {parameter ...,
        Takes the same arguments as {docref MsgDigest-of-len.default}.
    }
  }
  {constructor public {default ...:any}
    {construct-super.default ...}
  }
  
  {doc-next
    {purpose Construct from a {ctext StringInterface}.}
  }
  {constructor public implicit {from-string str:StringInterface}
    {construct-super.from-string str}
  }
  
  || String convenience functions 
  {doc-next {purpose Return a lower case string representing the digest.}}
  {method public {to-lower-String}:String
    {return
        {{String self}.to-lower-clone}
    }
  }
  
  {doc-next {purpose Return an upper case string representing the digest.}}
  {method public {to-upper-String}:String
    {return
        {{String self}.to-upper-clone}
    }
  }
  
  || clone
  {doc-next
    {purpose Creates a copy of the digest.}
    {return-vals 
        Returns a new sadMD5-Digest identical to the
        current object.
    }
  }
  {method public {clone}:sadMD5-Digest
    || create a new null digest (all zero bits)
    let result:sadMD5-Digest = {sadMD5-Digest}
    {result.xor-with self}
    {return result}
  }

  || combined-with
  {doc-next
    {purpose Returns a new digest representing the union of digests.}
    {parameter other,
        Another MD5 digest to be combined with this one.
    }
    {parameter ...,
        Additional sadMD5-Digests may also be specified.
    }
    {return-vals Returns the new digest.}
    {details
        Creates a new digest from the current digest and all of those
        passed in as arguments.  The state of the current digest
        and all of the arguments will not be changed.

        The ordering of the arguments is not significant.
    }
  }
  {method public {combined-with 
                     other:sadMD5-Digest, 
                     ...:sadMD5-Digest
                 }:sadMD5-Digest
    let result:sadMD5-Digest = {sadMD5-Digest}
    {result.xor-with self} || cute hack -- see clone
    {result.add-with other}
    {for yet-another in ... do
        {result.add-with yet-another}
    }
    {return result}
  }

  {getter public {bytes}:ByteVec
    let bv:ByteVec =
        {ByteVec.from-size self.words.size * 4, 0}
    
    {for word key i in self.words do
        let ii:int = 4*i
        {for b key j in {int32-to-ByteVec word} do
            set bv[ii + j] = b
        }
    }
    
    {return bv}
  }
} || sadMD5-Digest

{doc-next
    {purpose A message digester for {docref sadMD5-Digest}}
}
{define-class public final sadMD5-Digester 
  {inherits {MsgDigester-for sadMD5-Digest}}
  
  || constants for the calculation
  field private constant _R:{FastArray-of int32} =
      {{FastArray-of int32}
        7, 12, 17, 22,  7, 12, 17, 22,  7, 12, 17, 22,  7, 12, 17, 22,
        5,  9, 14, 20,  5,  9, 14, 20,  5,  9, 14, 20,  5,  9, 14, 20,
        4, 11, 16, 23,  4, 11, 16, 23,  4, 11, 16, 23,  4, 11, 16, 23,
        6, 10, 15, 21,  6, 10, 15, 21,  6, 10, 15, 21,  6, 10, 15, 21
      }

  field private constant _K:{FastArray-of int} =
      {{FastArray-of int}
        0xd76aa478, 0xe8c7b756, 0x242070db, 0xc1bdceee, 0xf57c0faf, 0x4787c62a, 0xa8304613, 0xfd469501,
        0x698098d8, 0x8b44f7af, 0xffff5bb1, 0x895cd7be, 0x6b901122, 0xfd987193, 0xa679438e, 0x49b40821,

        0xf61e2562, 0xc040b340, 0x265e5a51, 0xe9b6c7aa, 0xd62f105d, 0x2441453, 0xd8a1e681, 0xe7d3fbc8,
        0x21e1cde6, 0xc33707d6, 0xf4d50d87, 0x455a14ed, 0xa9e3e905, 0xfcefa3f8,0x676f02d9, 0x8d2a4c8a,

        0xfffa3942, 0x8771f681, 0x6d9d6122, 0xfde5380c, 0xa4beea44, 0x4bdecfa9, 0xf6bb4b60, 0xbebfbc70,
        0x289b7ec6, 0xeaa127fa, 0xd4ef3085, 0x4881d05, 0xd9d4d039, 0xe6db99e5, 0x1fa27cf8, 0xc4ac5665,

        0xf4292244, 0x432aff97, 0xab9423a7, 0xfc93a039, 0x655b59c3, 0x8f0ccc92, 0xffeff47d, 0x85845dd1,
        0x6fa87e4f, 0xfe2ce6e0, 0xa3014314, 0x4e0811a1, 0xf7537e82, 0xbd3af235, 0x2ad7d2bb, 0xeb86d391
      }

  || Message digest currently being computed
  field private _H0:int32
  field private _H1:int32
  field private _H2:int32
  field private _H3:int32


  || Current 512 bit message block
  field private constant _block:{FastArray-of byte} = 
      {new {FastArray-of byte}, max-size = 64}
  
  || Count of blocks processed so far.  Needed to
  || determine the message size (which is required
  || by the algorithm.  Negative value indicates that
  || digester is not started.
  field private _blocks-processed:int = -1

  || preallocated for use by process-block:
  field private constant _W:{FastArray-of int32} =
      {new {FastArray-of int32}.from-size, 16, 0}
  
  || init
  {doc-next
    {purpose Initialize the digester.}
    {parameter char-encoding,
        The character encoding to be used by 
        {docref sadMD5-Digester.add-char} and 
        related methods.  Also see {docref MsgDigester-for.default}.
    }
  }
  {constructor public {default char-encoding:CharEncoding=CharEncoding.utf8}
    {construct-super 
        char-encoding=char-encoding
    }
  }


  || start
  {doc-next
    {purpose Start the digest.}
    {details
        Produces an error if digester was already started.
    }
  }
  {method public final {start}:void
    {if self.started? then {error}}
    set self._H0 = 0x67452301
    set self._H1 = 0xEFCDAB89
    set self._H2 = 0x98BADCFE
    set self._H3 = 0x10325476

    set self._blocks-processed = 0
  }
  
  || started?
  {getter public inline final {started?}:bool
    {return self._blocks-processed >= 0}
  }

  || add-byte
  {method public inline final {add-byte c:byte}:void
    {self._block.append c}
    {if {self.block-full?} then
        {self.process-block}
    }
  }

  || finish
  {method public final {finish}:sadMD5-Digest
    {assert self.started?}
    let my-block:{FastArray-of byte} = self._block

    || Compute message size in bits
    let bits-in-msg:int32 = 
        (512 * self._blocks-processed) + (8 * my-block.size)
    
    || append a high-order '1' to block:
    {my-block.append 0x80}

    {if my-block.size > 56 then
        || not enough room for message length at end of block
        {while my-block.size < 64 do
            {my-block.append 0}
        }
        {self.process-block}
    }
    
    || write message length in last 64 bits of last block
    || (We go to 59 instead of 55 because we do not yet support
    ||  64 bit message lengths).
    {for i:int = my-block.size to 63 do
        {my-block.append 0}
    }

    let bv:ByteVec = {int32-to-ByteVec bits-in-msg}
    {for i = 0 below 4 do
        set my-block[i + 56] = bv[i]
    }   
    
    {self.process-block}
    
    set self._blocks-processed = -1

    || create the digest by encding the bytes of the state variables
    let md5-digest:sadMD5-Digest = 
        {sadMD5-Digest 
            {int32-reverse self._H0}, 
            {int32-reverse self._H1}, 
            {int32-reverse self._H2}, 
            {int32-reverse self._H3}
        }
    


    {return md5-digest}
    
  } || finish

  || block-full?
  {method inline private {block-full?}:bool
    {return 64 == self._block.size}
  }
  
  || process-block
  {method inline private {process-block}:void
    {assert {self.block-full?}}

    let my-block:{FastArray-of byte} = self._block

    || Encode the current block
    let W:{FastArray-of int32} = self._W
    {for t = 0 to 15 do
        let i:int = t * 4
        let bv:ByteVec = 
            {ByteVec
                my-block[i + 0],my-block[i + 1],my-block[i + 2],my-block[i + 3]
            }

        set W[t] = {ByteVec-to-int32 bv} 
    }

    || Update the state variables
    {let 
        a:int32 = self._H0,
        b:int32 = self._H1,
        c:int32 = self._H2,
        d:int32 = self._H3,
        f:int32 = 0,
        g:int32 = 0,
        temp:int32 = 0
    }

    || this code is derived from the Wikipedia entry.
    {for i = 0 to 63 do
        {if 0 <= i and i <= 15 then
            set f = {self.F b,c,d}
            set g = i
         elseif 16 <= i and i <= 31 then
            set f = {self.G b,c,d}
            set g = (5*i + 1) mod 16
         elseif 32 <= i and i <= 47 then
            set f = {self.H b,c,d}
            set g = (3*i + 5) mod 16
         elseif 48 <= i and i <= 63 then
            set f = {self.I b,c,d}
            set g = (7*i) mod 16
        }
        || accumulate the state variables
        set temp = d
        set d = c
        set c = b
        set b = {bit-rol (a + f + self._K[i] + W[g]), self._R[i]} + b
        set a = temp
    }

    || store the state variables
    set self._H0 = self._H0 + a
    set self._H1 = self._H1 + b
    set self._H2 = self._H2 + c
    set self._H3 = self._H3 + d
    
    {my-block.clear}
    {set self._blocks-processed = self._blocks-processed + 1}
    
  } || process-block
  
  || 
  {method inline private {F x:int32, y:int32, z:int32}:int32
    {return
        {bit-and 0xffffffff, 
            {bit-or {bit-and x,y}, {bit-and {bit-not x}, z}}
        }
    }
  }
  
  {method inline private {G x:int32, y:int32, z:int32}:int32
    {return
        {bit-and 0xffffffff, 
            {bit-or {bit-and x, z}, {bit-and y, {bit-not z}}}
        }
    }
  }

  {method inline private {H x:int32, y:int32, z:int32}:int32
    {return
        {bit-and 0xffffffff, 
            {bit-xor x,y,z}
        }
    }
  }

  {method inline private {I x:int32, y:int32, z:int32}:int32 
    {return
        {bit-and 0xffffffff, 
            {bit-xor y, {bit-or x, {bit-not z}}}
        }
    }
  }

} || sadMD5-Digester

||=======================
|| sadMD5-ByteInputStream
||=======================

{doc-next
    {purpose {docref ByteInputStream} filter that computes MD5 digest}
    {details
        This class is used to filter an existing {docref ByteInputStream}
        to compute a {docref sadMD5-Digest} based on the bytes read from
        the underlying stream.

        This should produce the same digest as that produced by 
        {docref sadMD5-ByteOutputStream} for the same sequence of bytes.
    }
    {for-example
        {curl-code
            let digesting-stream:sadMD5-ByteInputStream =
                {sadMD5-ByteInputStream existing-stream}
            {do-something-with-stream digesting-stream}
            {digesting-stream.close}
            let digest:sadMD5-Digest = digesting-stream.digest
        }
    }
}
{define-class public final sadMD5-ByteInputStream
  {inherits {InputStream-of byte}}

  field private _digester:sadMD5-Digester

  field private _stream:#ByteInputStream

  field private constant close-stream-on-close?:bool

  {doc-next
    {purpose The computed MD5 message digest.}
    {details
        Will be null until {ctext self.finish-digest} is called, which
        happens automatically when the stream is closed.
    }
  }
  field public-get private-set digest:#sadMD5-Digest

  ||--------------
  || Construction
  ||--------------

  {doc-next
    {purpose Initialize stream.}
    {parameter stream, the underlying output stream.  All operations
        will be dispatched to this object.
    }
    {parameter close-stream-on-close?, 
        specifies whether this class's {ctext close} method should invoke 
        {ctext close} on the underlying stream.
    }
  }
  {constructor public {default
                          stream:ByteInputStream,
                          close-stream-on-close?:bool = true
                      }
    set self._stream = stream
    set self.close-stream-on-close? = close-stream-on-close?
    set self._digester = {sadMD5-Digester}
    {self._digester.start}
    {construct-super}
  }

  ||------------------
  || Field access
  ||------------------

  {doc-next
    {purpose The underlying stream object.}
  }
  {getter public {stream}:ByteInputStream
    ||{self.verify-open}
    {return {non-null self._stream}}
  }

  ||------------------
  || Stream interface
  ||------------------

  {doc-next
    {purpose Implement read operations}
    {details
        Reads from underlying stream, and if successful adds
        byte to message digester.
    }
  }
  {method public final {read-one}:(b:byte, eof?:bool)
    let (b:byte, eof?:bool) = {self.stream.read-one}
    {if not eof? and self._digester.started? then
        {self._digester.add-byte b}
    }
    {return b, eof?}
  }

  {getter public final {open?}:bool
    {return self._stream != null}
  }

  {doc-next
    {purpose Closes stream and finishes message digest.}
  }
  {method public final {close}:void
    {if-non-null stream = self._stream then
        set self._stream = null
        {try
            {self.finish-digest}
         finally
            {if self.close-stream-on-close? then
                {stream.close}
            }
        }
    }
  }

  || doc inherited
  {getter public final {origin-url}:#Url
    {return self.stream.origin-url}
  }

  || Doc-string inherited
  {getter public {when-last-modified}:#DateTime
    {return self.stream.when-last-modified}
  }

  
  ||-------------
  || MsgDigester
  ||-------------

  {doc-next
    {purpose Finishes computation of message digest}
    {details
        All bytes read after invoking this will not be
        reflected in the digest.
    }
  }
  {method public final {finish-digest}:sadMD5-Digest
    || This method can be called after the stream has been closed.  Of
    || course, closing the stream will already have set self.digest,
    || so such a call will just return the already-computed digest.
    {if-non-null digest = self.digest then
        {return digest}
     else
        let digest:sadMD5-Digest = {self._digester.finish}
        set self.digest = digest
        {return digest}
    }
  }

} || sadMD5-ByteInputStream


||========================
|| sadMD5-ByteOutputStream
||========================

{doc-next
    {purpose {docref ByteOutputStream} filter that computes MD5 digest}
    {details
        This class is used to filter an existing {docref ByteOutputStream}
        to compute a {docref sadMD5-Digest} based on the bytes written to
        the underlying stream.
    }
    {for-example
        {curl-code
            let digesting-stream:sadMD5-ByteOutputStream =
                {sadMD5-ByteOutputStream existing-stream}
            {do-something-with-stream digesting-stream}
            {digesting-stream.close}
            let digest:sadMD5-Digest = digesting-stream.digest
        }
    }
}
{define-class public final sadMD5-ByteOutputStream {inherits ByteOutputStream}

  ||-------
  || State
  ||-------

  field private constant _digester:sadMD5-Digester

  field private _stream:#ByteOutputStream
  
  field private constant close-stream-on-close?:bool

  {doc-next
    {purpose The computed MD5 message digest.}
    {details
        Will be null until the stream is closed.
    }
  }
  field public-get private-set digest:#sadMD5-Digest

  ||--------------
  || Construction
  ||--------------

  {doc-next
    {purpose Initialize stream.}
    {parameter stream, the underlying output stream.  All operations
        will be dispatched to this object.
    }
    {parameter close-stream-on-close?, 
        specifies whether this class's {ctext close} method should invoke 
        {ctext close} on the underlying stream.
    }
  }
  {constructor public {default 
                          stream:ByteOutputStream,
                          close-stream-on-close?:bool = true
                      }
    set self._stream = stream
    set self.close-stream-on-close? = close-stream-on-close?
    set self._digester = {sadMD5-Digester}
    {self._digester.start}
    {construct-super}
  }

  ||------------------
  || Field access
  ||------------------

  {doc-next
    {purpose The underlying stream object.}
  }
  {getter public {stream}:ByteOutputStream
    ||{self.verify-open}
    {return {non-null self._stream}}
  }

  ||------------------
  || Stream interface
  ||------------------

  {doc-next
    {purpose Implement write operations}
    {details
        Adds byte to message digester and writes to underlying stream.
    }
  }
  {method public final {write-one b:byte}:void
    {self.stream.write-one b}
    {self._digester.add-byte b}
  }

  {getter public final {open?}:bool
    {return self._stream != null}
  }

  {doc-next
    {purpose Closes stream and finishes message digest.}
  }
  {method public final {close}:void
    {if-non-null stream = self._stream then
        set self._stream = null
        {try
            {self.finish-digest}
         finally
            {if self.close-stream-on-close? then
                {stream.close}
            }
        }
    }
  }

  {method public final {flush
                           non-blocking?:bool = false,
                           allow-short-write?:bool = false
                       }:void
    {self.stream.flush
        non-blocking? = non-blocking?, allow-short-write? = allow-short-write?
    }
  }

  || doc inherited
  {getter public final {origin-url}:#Url
    {return self.stream.origin-url}
  }

  ||-------------
  || MsgDigester
  ||-------------

  {doc-next
    {purpose Finishes computation of message digest}
    {details
        All bytes added after invoking this will not be
        reflected in the digest.
    }
  }
  {method public final {finish-digest}:sadMD5-Digest
    || This method can be called after the stream has been closed.  Of
    || course, closing the stream will already have set self.digest,
    || so such a call will just return the already-computed digest.
    {if-non-null digest = self.digest then
        {return digest}
     else
        let digest:sadMD5-Digest = {self._digester.finish}
        set self.digest = digest
        {return digest}
    }
  }

} || sadMD5-ByteOutputStream



